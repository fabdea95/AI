(define (domain dominioEjercicios)
    (:requirements :strips :adl :typing)
    (:types ejercicio obj_orden - object
            nivel dia - obj_orden
            preparador predecesor - ejercicio)
    
    (:predicates    (es_preparador ?x - ejercicio ?y - ejercicio)
                    (es_predecesor ?x - ejercicio ?y - ejercicio)
                    (se_ejercita ?x - ejercicio)
                    (se_ejercita_en ?x - ejercicio ?d - dia)
                    (se_ejercita_con ?x - ejercicio ?n - nivel)
                    (anterior ?d0 - obj_orden ?d1 - obj_orden)
                    (ultimo_ejercicio_dia ?d - dia ?x - ejercicio)
    )
    
    
    (:action ejercitar_ejercicio 
        :parameters (?x - ejercicio ?n - nivel ?d - dia)
        :precondition   (and    (not (se_ejercita_con ?x ?n))
                                (not (se_ejercita_en ?x ?d))
                                (forall (?y - preparador)
                                        (imply (es_preparador ?y ?x)
                                        (se_ejercita_en ?y ?d))
                                )
                                (forall (?y - predecesor)
                                    (imply (es_predecesor ?y ?x)
                                            (ultimo_ejercicio_dia ?d ?y)
                                    )
                                )
                                (imply (se_ejercita ?x)
                                        (exists (?n1 - nivel ?d1 - dia) (and (anterior ?d1 ?d) (anterior ?n1 ?n) (se_ejercita_en ?x ?d1) (se_ejercita_con ?x ?n1)))
                                )
                        )
                                        
        :effect (and (se_ejercita ?x) (se_ejercita_con ?x ?n) (se_ejercita_en ?x ?d)
                    (forall (?y - ejercicio)
                        (when (ultimo_ejercicio_dia ?d ?y) (not (ultimo_ejercicio_dia ?d ?y)))
                    )
                    (ultimo_ejercicio_dia ?d ?x) 
        )
    )
)